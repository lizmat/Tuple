use Test;
use Tuple;

plan 27;

my @a is Tuple = ^10;
is-deeply @a.Bool, True, 'a Tuple with elements is True';

my @b is Tuple = ^10;
is @b.WHICH, @a.WHICH, 'are @b and @a the same value type';

my $c = tuple(^10);
is $c.WHICH, @a.WHICH, 'are $c and @a the same value type';

my $d = Tuple.new(^10);
is $d.WHICH, @a.WHICH, 'are $d and @a the same value type';

my $e = Tuple.new(@a);
is $e.WHICH, @a.WHICH, 'are $e and @a the same value type';

my $f = tuple(@a);
is $f.WHICH, @a.WHICH, 'are $f and @a the same value type';

is $f.gist, 'Tuple.new(0 1 2 3 4 5 6 7 8 9)', 'does .gist give correct output';
is $f.Str, $f.raku,  'does .Str give the same as .raku';

dies-ok { @a = 4,5,6 },  'can we not re-assign to a tuple';
dies-ok { @a[0] = 42 },  'can we not assign to a tuple element';
dies-ok { @a[0] := 42 }, 'can we not bind to a tuple element';

for <
  ASSIGN-POS BIND-POS push append pop shift unshift prepend
> -> $method {
    dies-ok { @a."$method"() }, "does calling .$method die";
}

my %s is Set = tuple(^2), tuple(^2);
is %s.elems, 1, 'did we get 1 tuple with "is Set"';
is set( tuple(^2), tuple(^2) ).elems, 1, 'did we get 1 tuple "set()"';

dies-ok { $_ = 42 for @a },
  'are iterated values also immutable';
dies-ok { .value = 42 for @a.pairs },
  'are iterated pairs also immutable';
dies-ok { for @a.kv -> \k, \v { v = 42 } },
  'are iterated kv also immutable';

my @g is Tuple;
is-deeply @g.Bool, False, 'an empty Tuple @g is False';

my $h = Tuple.new;
is-deeply $h.Bool, False, 'an empty Tuple $h is False';

my $i = Tuple.new(1,2,3);
is-deeply $i, tuple(1,2,3), 'Tuple.new with args ok';

# vim: expandtab shiftwidth=4
